#!/usr/bin/perl

# TODO
# * Implement word option
# * Differentiate title and allow for title search
# * allow input from file

use strict;
use warnings FATAL => 'all';

use Data::Dumper;
use Getopt::Long;
use IO::File;

our $VERSION = '0.3';

my $title      = '';
my $edit       =  0;
my $word       =  0;
my $add        =  0;
my $help       =  0;

my $notefile = "$ENV{HOME}/info/notes.txt";
my $separator = '%%%';

GetOptions(
    "title=s" => \$title,
    "word"    => \$word,
    "add"     => \$add,
    "help"    => \$help,
    "edit"    => \$edit,
) or die("Error in command line arguments\n");

sub usage {
    return <<EOF;
Usage: note [OPTION]... PATTERN...
Add or retrieve a note
Example: note lawnmower

Flags for add:
-a, --add       Add a note
-t, --title     Title for the note being added

Flags for search:
-w, --word      Only find if PATTERN is a word

Other flags:
-e, --edit      Edit the notes file
EOF
}

sub errout {
}

sub edit_notes {
    my $editor = $ENV{EDITOR} || 'vim';
    system("$editor $notefile");
}

sub search_note {
    my (@patterns) = @_;

    my $ifh = IO::File->new( $notefile, '<' );
    die if ( !defined $ifh );

    my $contents = do { local $/; <$ifh> };

    $ifh->close;

    my @notes = split /^$separator/m, $contents;

    my @lines;
    OUTER: for (@notes) {
        for my $pattern (@patterns) {
            next OUTER unless /$pattern/i;
        }
        s/^/  /mg;
        push @lines, "$_";
    }
    my $output_separator = '+' . '=' x 78 . "\n|";
    if (@lines) {
        my $output = join( "$output_separator\n", @lines );
        if ( @lines == 1 ) {
            my $cfh = IO::File->new('/dev/clipboard', '>');
            die if (!defined $cfh);

            print $cfh "$output";
            $cfh->close;

        }
        print $output;
    }
    else {
        print "No match.\n";
    }
}

sub make_note {
    print "Make note:\n";

    my $note;
    while (<STDIN>) {
        $note .= $_;
    }

    my $ofh = IO::File->new( $notefile, '>>' );
    die if ( !defined $ofh );

    print $ofh "${separator}${note}\n";
    $ofh->close;
}

sub main {
    my (@patterns) = @_;

    if ($help) {
        die usage();
    }
    elsif ($edit) {
        edit_notes();
    }
    elsif (@patterns) {
        search_note(@patterns);
    }
    elsif ($add) {
        make_note();
    }
    else {
        die usage();
    }
    return;
}

my $rc = ( main(@ARGV) || 0 );

exit $rc;

