#!/usr/bin/perl

# TODO
# * allow for title search
# * implement tags
# * implement delete entry
# * implement edit individual entry
# * allow input from file

# DONE
# * -1 and -2 switch for which note file to use
# * Implement word option

use strict;
use warnings FATAL => 'all';

use Data::Dumper;
use Getopt::Long;
use IO::File;

my $nf = $ENV{NOTEFILES} || "$ENV{HOME}/info/notes.txt";
my @notefiles = split ':', $nf;

our $VERSION = '0.9';

my $one   = '';
my $two   = '';
my $title = '';
my $edit  =  0;
my $word  =  0;
my $add   =  0;
my $help  =  0;

my $separator = "%%%\n";

GetOptions(
    "1"       => \$one,
    "2"       => \$two,
    "title"   => \$title,
    "word"    => \$word,
    "add"     => \$add,
    "help"    => \$help,
    "edit"    => \$edit,
) or die("Error in command line arguments\n");

@notefiles = $notefiles[0] if $one;
@notefiles = $notefiles[1] if $two;

sub usage {
    return <<EOF;
Usage: note [OPTION]... PATTERN...
Add or retrieve a note
Example: note lawnmower

-1, --1         use notefile #1
-2, --2         use notefile #2
-a, --add       Add a note
-t, --title     Title for the note being added
-w, --word      Only find if PATTERN is a word
-e, --edit      Edit the notes file
EOF
}

sub errout {
}

sub edit_notes {
    my $editor = $ENV{EDITOR} || 'vim';
    my $paths = join(' ', @notefiles);
    system("$editor $paths");
}

my @output_lines;
sub output {
    my $output_line = join('',@_);
    push @output_lines, $output_line;
}

sub search_note {
    my (@patterns) = @_;

    if ( $word ) {
        for (@patterns) {
            $_ = "\\b$_\\b";
        }
    }

    if ( $title ) {
    }

    my @notes;
    for my $file (@notefiles) {
        my $ifh = IO::File->new( $file, '<' );
        die if ( !defined $ifh );

        my $contents = do { local $/; <$ifh> };

        $ifh->close;
        push @notes, split(/^$separator/m, $contents);
    }

    my %matches;
    OUTER: for (@notes) {
        for my $pattern (@patterns) {
            next OUTER unless /$pattern/i;
        }
        s/^/  /mg;
        $matches{$_}++;
    }
    my $output_separator = "\n" . '+' . '=' x 78 . "\n|";
    my $title_separator  = "\n" . '+' . '-' x 68 . "\n";
    my $gpg_file = "$ENV{HOME}/.note.gpg";
    if (%matches) {
        for my $match (keys %matches) {
            my @lines = split "\n", $match;
            my $title = shift @lines;
            $title =~ s/^\s*//;
            my $body = join "\n", @lines;
            $body =~ s/\s*$//s;
            output $output_separator,
                   " $title",
                   $title_separator;
            if ($body =~ /BEGIN PGP MESSAGE.*END PGP MESSAGE/s) {
                my $ofh = IO::File->new($gpg_file, '>');
                die if (!defined $ofh);
                $body =~ s/^ *//mg;
                print $ofh $body;
                $ofh->close;
                if ( -r $gpg_file ) {
                    system("gpg --decrypt $gpg_file && rm $gpg_file");
                }
            }
            else {
                output $body, "\n";
                my $cfh = IO::File->new('/dev/clipboard', '>');
                if (defined $cfh) {
                    print $cfh $body;
                    $cfh->close;
                }
            }
        }
        output "\n";
    }
    else {
        output "No match.\n";
    }
    open O, '| less -FX' or die;
    print O @output_lines;
    close O;
}

sub add_note {
    print "Make note:\n";

    my $note_title = "$title\n";
    my $note;
    while (my $input_line = <STDIN>) {
        $note .= $input_line;
        if ( $note_title !~ /\w/ && $input_line =~ /\w/ ) {
            $note_title = $input_line;
        }
    }

    my $ofh = IO::File->new( $notefiles[0], '>>' );
    die if ( !defined $ofh );

    print $ofh "${separator}${note_title}${note}\n";
    $ofh->close;
}

sub main {
    my (@patterns) = @_;

    if    ($help)     { die usage()              }
    elsif ($edit)     { edit_notes()             }
    elsif (@patterns) { search_note( @patterns ) }
    elsif ($add)      { add_note()               }
    else              { die usage()              }

    return;
}

my $rc = ( main(@ARGV) || 0 );

exit $rc;

